#lang racket

;;;; fast modular exponentiation. From the textbook, section 1.2

(define (expmod b e m) ;; liczymy b^e mod m
  (cond ((zero? e) 1)
        ((even? e)
         (remainder (square (expmod b (/ e 2) m)) ;;sprawdzamy parzystość
                    m))
        (else
         (remainder (* b (expmod b (- e 1) m))
                    m))))

(define (square x) (* x x))


;;; An RSA key consists of a modulus and an exponent. (moduł i wykładnik)

(define make-key cons) ;; konstruktor
(define key-modulus car) ;; selektor modułu
(define key-exponent cdr) ;; selektor wykładnika

(define (RSA-transform number key)
  (expmod number (key-exponent key) (key-modulus key)))


;;;; generating RSA keys

;;; To choose a prime, we start searching at a random odd number in a
;;; specifed range

(define (choose-prime smallest range)
  (let ((start (+ smallest (choose-random range))))
    (search-for-prime (if (even? start) (+ start 1) start))))

(define (search-for-prime guess)
  (if (fast-prime? guess 2)
      guess
      (search-for-prime (+ guess 2))))

;;; The following procedure picks a random number in a given range,
;;; but makes sure that the specified range is not too big for
;;; Scheme's RANDOM primitive.

(define choose-random
  ;; restriction of Scheme RANDOM primitive
  (let ((max-random-number (expt 10 18))) 
    (lambda (n)
      (random (floor (min n max-random-number))))))


;;; The Fermat test for primality, from the texbook section 1.2.6

(define (fermat-test n)
    (let ((a (choose-random n)))
      (= (expmod a n n) a)))

(define (fast-prime? n times)
    (cond ((zero? times) true)
          ((fermat-test n) (fast-prime? n (- times 1)))
          (else false)))


;;; RSA key pairs are pairs of keys

(define make-key-pair cons)
(define key-pair-public car)
(define key-pair-private cdr)

;;; generate an RSA key pair (k1, k2).  This has the property that
;;; transforming by k1 and transforming by k2 are inverse operations.
;;; Thus, we can use one key as the public key andone as the private key.

(define (generate-RSA-key-pair)
  (let ((size (expt 2 14)))
    ;; we choose p and q in the range from 2^14 to 2^15.  This insures
    ;; that the pq will be in the range 2^28 to 2^30, which is large
    ;; enough to encode four characters per number.
    (let ((p (choose-prime size size))
          (q (choose-prime size size)))
    (if (= p q)       ;check that we haven't chosen the same prime twice
        (generate-RSA-key-pair)     ;(VERY unlikely)
        (let ((n (* p q))
              (m (* (- p 1) (- q 1))))
          (let ((e (select-exponent m)))  ;; e względnie pierwsze z m
            (let ((d (invert-modulo e m)))
              (make-key-pair (make-key n e) (make-key n d)))))))))


;;; The RSA exponent can be any random number relatively prime to m

(define (select-exponent m)
  (let ((try (choose-random m)))
    (if (= (gcd try m) 1)
        try
        (select-exponent m))))


;;; Invert e modulo m

(define (invert-modulo e m)
  (if (= (gcd e m) 1)
      (let ((y (cdr (solve-ax+by=1 m e))))
        (modulo y m))                   ;just in case y was negative
      (error "gcd not 1" e m)))


;;; solve ax+by=1
;;; The idea is to let a=bq+r and solve bx+ry=1 recursively
;; Skorzystałem z algorytmu Number Theory: Diophantine Equation: ax+by=gcd(a,b), znalezioneogo na yt

(define (solve-ax+by=1 a b)
  (define (helper a b)
    (/ (- a (modulo a b)) b))
  (cond
    ((= b 0)
    (cond ((= a 1) (cons 1 0))
          ((= a -1) (cons -1 0))
          (else "wrong_data")))
    (else
     (cons (cdr (solve-ax+by=1 b (remainder a b))) ;;w tym miejscu rekurencyjnie zliczamy pozostałe wcześniej elementy
           (- (car (solve-ax+by=1 b (remainder a b)))
              (* (cdr (solve-ax+by=1 b (remainder a b))) (helper a b))))))) 
  
  
  

;;; Actual RSA encryption and decryption

(define (RSA-encrypt string key1)
  (RSA-convert-list (string->intlist string) key1))

(define (RSA-convert-list intlist key)
  (let ((n (key-modulus key)))
    (define (convert l sum)
      (if (null? l)   ;; jeśli zakodowana lista jest pusta, zwracamy pustą listę
          '()          
          (let ((x (RSA-transform (modulo (- (car l) sum) n) ;; przekształcamy element
                                  key)))
            (cons x (convert (cdr l) x))))) ;; dodajemy do nowej listy i powtarzamy z kolejnym
    (convert intlist 0)))

(define (RSA-decrypt intlist key2)
  (intlist->string (RSA-unconvert-list intlist key2)))

(define (RSA-unconvert-list intlist key)
  (let ((n (key-modulus key)))
    (define (unconvert l sum)
      (if (null? l)
          '()
          (let ((x (modulo (+ sum (RSA-transform (car l) key)) n))) ;;najpierw przekształcamy, a potem dodajemy do sumy i robimy mod n
            (cons x (unconvert (cdr l) (car l))))))        ;; operacje są w odwrotnej kolejności nić RSA-convert
    (unconvert intlist 0)))


;;;; Digital signatures

;;; The following routine compresses a list of numbers to a single
;;; number for use in creating digital signatures.

(define (compress intlist)
  (define (add-loop l)
    (if (null? l)
        0
        (+ (car l) (add-loop (cdr l)))))
  (modulo (add-loop intlist) (expt 2 28)))


;;;XXX: Define the data structure that represents signed messages here:
(define (make-signed-message signature message)
  (cons signature message))

(define (signature signed-message)
  (car signed-message))

(define (message signed-message)
  (cdr signed-message))

;;; Encrypting and signing a message

(define (encrypt-and-sign message recipient-public-key sender-private-key)
  (let ((secret-code (RSA-encrypt message recipient-public-key)))
    (make-signed-message (car (RSA-convert-list (list (compress secret-code)) sender-private-key)) secret-code)))
  

(define (authenticate-and-decrypt cyphertext recipient-private-key sender-public-key)
  (let ((text (RSA-unconvert-list (message cyphertext) recipient-private-key)))
    (let ((real_signature (compress (message cyphertext))))
      (let ((claimed_signature (car(RSA-unconvert-list (list (signature cyphertext)) sender-public-key))))
        (if (= real_signature claimed_signature)
            (intlist->string text)
            "wrong key")))))

;;;; searching for divisors.

;;; The following procedure is very much like the find-divisor
;;; procedure of section 1.2 of the text, except that it increments
;;; the test divisor by 2 each time.  You should be careful to call
;;; it only with odd numbers n.

(define (smallest-divisor n)
  (find-divisor n 3))

(define (find-divisor n test-divisor)
  (cond ((> (square test-divisor) n) n)
        ((divides? test-divisor n) test-divisor)
        (else (find-divisor n (+ test-divisor 2)))))

(define (divides? a b)
  (= (remainder b a) 0))



;;;; converting between strings and numbers

;;; The following procedures are used to convert between strings, and
;;; lists of integers in the range 0 through 2^28.  You are not
;;; responsible for studying this code -- just use it.

;;; Convert a string into a list of integers, where each integer
;;; encodes a block of characters.  Pad the string with spaces if the
;;; length of the string is not a multiple of the blocksize.

(define (string->intlist string)
  (let ((blocksize 4))
    (let ((padded-string (pad-string string blocksize)))
      (let ((length (string-length padded-string)))
        (block-convert padded-string 0 length blocksize)))))

(define (block-convert string start-index end-index blocksize)
  (if (= start-index end-index)
      '()
      (let ((block-end (+ start-index blocksize)))
        (cons (charlist->integer
	       (string->list (substring string start-index block-end)))
              (block-convert string block-end end-index blocksize)))))

(define (pad-string string blocksize)
  (let ((rem (remainder (string-length string) blocksize)))
    (if (= rem 0)
        string
        (string-append string (make-string (- blocksize rem) #\Space)))))

;;; Encode a list of characters as a single number
;;; Each character gets converted to an ascii code between 0 and 127.
;;; Then the resulting number is c[0]+c[1]*128+c[2]*128^2,...

(define (charlist->integer charlist)
  (let ((n (char->integer (car charlist))))
    (if (null? (cdr charlist))
        n
        (+ n (* 128 (charlist->integer (cdr charlist)))))))

;;; Convert a list of integers to a string. (Inverse of
;;; string->intlist, except for the padding.)

(define (intlist->string intlist)
  (list->string
   (apply
    append
    (map integer->charlist intlist))))



;;; Decode an integer into a list of characters.  (This is essentially
;;; writing the integer in base 128, and converting each "digit" to a
;;; character.)

(define (integer->charlist integer)
  (if (< integer 128)
      (list (integer->char integer))
      (cons (integer->char (remainder integer 128))
            (integer->charlist (quotient integer 128)))))

;;;; Some initial test data

(define test-key-pair1
  (make-key-pair
   (make-key 816898139 180798509)
   (make-key 816898139 301956869)))

(define test-key-pair2
  (make-key-pair
   (make-key 513756253 416427023)
   (make-key 513756253 462557987)))

;; Testy szyfrowania, używam kluczy (791005613 87119639), (791005613 23429159), które uzyskałem używając funkcji (generate-RSA-key-pair)

(RSA-encrypt "metody programowania zadanie z szyframi RSA" (make-key 791005613 87119639))

(RSA-decrypt '(274533363
  788451247
  121902791
  387681117
  422610675
  3369838
  682939877
  589174634
  544711577
  566776273
  519301712) (make-key 791005613 23429159))

(encrypt-and-sign "testy kodowania i podpisywania wiadomosci" (make-key 704923489 176487471) (make-key 791005613 23429159))

(authenticate-and-decrypt '(45415957
  689114562
  703496662
  444889763
  100379831
  520247047
  45536225
  612784945
  470250089
  28231169
  454240694
  475484116) (make-key 704923489 237196511) (make-key 791005613 87119639)) ;; prawidłowy klucz- poprawne deszyfrowywanie


(authenticate-and-decrypt '(45415957
  689114562
  703496662
  444889763
  100379831
  520247047
  45536225
  612784945
  470250089
  28231169
  454240694
  475484116) (make-key 704923489 237196511) (make-key 791005613 87119600)) ;; nieprawidłowy klucz- błąd







